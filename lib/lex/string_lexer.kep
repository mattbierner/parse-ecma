/**
 * @fileOverview Lexers for ECMAScript 5.1 string values.
 */
package (
// Constants
    doubleQuote
    escape
    singleQuote
    lineContinuation
    
// Escape Sequences
    singleEscapeCharacter
    escapeCharacter
    nonEscapeCharacter
    characterEscapeSequence
    unicodeEscapeSequence
    hexEscapeSequence
    escapeSequence
    
// Single String Literal
    singleStringCharacter
    singleStringCharacters
    singleStringLiteral
    
// Double String Literal
    doubleStringCharacter
    doubleStringCharacters
    doubleStringLiteral

// Literal
    stringLiteral)
with
    import 'bennu::parse' {
        always
        anyToken
        attempt
        choice
        either
        label
        map
        many
        next
        not
        eof
        sequence},
    import 'bennu::lang' {
        between
        times},
    import 'bennu::text' {
        character
        oneOf
        noneOf},
    
    import 'nu-stream::stream' {foldl},
    
    import './line_terminator_lexer' {
        lineTerminator
        lineTerminatorSequence},
    import './number_lexer' {
        decimalDigit
        hexDigit}
in {

var join := map@(foldl @ (+) @ '');

var fromCharCodeParser := join \> map @ \x -> String.fromCharCode(parseInt(x, 16));

/* Lexers
 ******************************************************************************/
// Constants
// //////////////////////////////////////
doubleQuote := character '"';
escape := character '\\';
singleQuote := character "'";

/**
 * Lexer for string line continuation.
 * 
 * Returns an empty string.
 */
lineContinuation :=
    sequence(
        escape,
        lineTerminatorSequence,
        always '');

// Escape Character
// //////////////////////////////////////
/**
 * Lexer for a single escape character.
 * 
 * Returns the value represented by the escape character.
 */
singleEscapeCharacter := choice(
    character "'",
    character '"',
    character '\\',
    next(character 'b', always('\u0008')),
    next(character 'f', always('\u000C')),
    next(character 'n', always('\u000A')),
    next(character 'r', always('\u000D')),
    next(character 't', always('\u0009')),
    next(character 'v', always('\u000B')));

escapeCharacter :=
    choice(
        singleEscapeCharacter,
        decimalDigit,
        oneOf 'ux');

nonEscapeCharacter :=
    sequence(
        not escapeCharacter,
        not lineTerminator,
        anyToken);

// Escape Sequence
// //////////////////////////////////////
/**
 * Hex escape sequence in a string.
 * 
 * Returns the character defined by the escape sequence.
 */
hexEscapeSequence :=
    next(
        character 'x',
        hexDigit |> times@2 |> fromCharCodeParser);

/**
 * Unicode escape sequence in a string.
 * 
 * Returns the unicode character defined by the escape sequence.
 */
unicodeEscapeSequence :=
    next(
        character 'u',
        hexDigit |> times@4 |> fromCharCodeParser);

/**
 * Character escape sequence in a string.
 */
characterEscapeSequence :=
    either(
        singleEscapeCharacter,
        nonEscapeCharacter);

/**
 * Escape sequence in a string.
 * 
 * Returns the character defined by the escape sequence.
 */
escapeSequence :=
    choice(
        characterEscapeSequence,
        sequence(
            character '0',
            not decimalDigit,
            always '\u0000'),
        hexEscapeSequence,
        unicodeEscapeSequence);

// Single String Literal
// //////////////////////////////////////
/**
 * Character in a single quoted string.
 */
singleStringCharacter :=
    choice(
        attempt lineContinuation,
        next(
            escape,
            escapeSequence),
        next(
            not lineTerminator,
            noneOf "\\'"));

/**
 * Single quoted string characters.
 */
singleStringCharacters := many singleStringCharacter;

/**
 * Single quoted string literal.
 * 
 * Returns the value of the string enclosed in the single quoted string literal.
 */
singleStringLiteral := label@'Single String Literal' <|
    between(singleQuote, singleQuote,
        join singleStringCharacters);

// Double String Literal
// //////////////////////////////////////
/**
 * Character in a double quoted string.
 */
doubleStringCharacter :=
    choice(
        attempt lineContinuation,
        next(
            escape,
            escapeSequence),
        next(
            not lineTerminator,
            noneOf '"\\'));

/**
 * Double quoted string characters.
 */
doubleStringCharacters := many doubleStringCharacter;

/**
 * Double quoted string literal.
 * 
 * Returns the value of the string enclosed in the double quoted string literal.
 */
doubleStringLiteral := label@'Double String Literal' <|
    between(doubleQuote, doubleQuote,
        join doubleStringCharacters);

// String Literal
// //////////////////////////////////////
/**
 * String literal.
 * 
 * Returns the value of the string enclosed in the string literal.
 */
stringLiteral := label@'Sting Literal Lexer' <|
    either(
        singleStringLiteral,
        doubleStringLiteral);

}